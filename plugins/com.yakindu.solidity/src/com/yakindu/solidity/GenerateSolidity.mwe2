/**
 * Copyright (c) 2017 committers of YAKINDU and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 * 
 * Contributors:
 * 	Andreas Muelder - Itemis AG - initial API and implementation
 * 	Karsten Thoms   - Itemis AG - initial API and implementation
 * 	Florian Antony  - Itemis AG - initial API and implementation
 * 	committers of YAKINDU 
 * 
 */
module com.yakindu.solidity.GenerateSolidity

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow {

	bean = org.eclipse.emf.mwe.utils.StandaloneSetup {
		scanClassPath = true
		platformUri = "."
		registerGenModelFile = "platform:/resource/org.yakindu.base.types/model/base.genmodel"
		registerGenModelFile = "platform:/resource/org.yakindu.base.types/model/types.genmodel"
		registerGenModelFile = "platform:/resource/org.yakindu.base.expressions/model/Expressions.genmodel"
		registerEcoreFile = "platform:/resource/org.yakindu.base.types/model/base.ecore"
		registerEcoreFile = "platform:/resource/org.yakindu.base.types/model/types.ecore"
		registerEcoreFile = "platform:/resource/org.yakindu.base.expressions/model/Expressions.ecore"
	}

	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "com.yakindu.solidity"
				rootPath = rootPath
				eclipsePlugin = {
					enabled = true
				}
				genericIde = {
					enabled = true
				}
				createEclipseMetaData = true
				runtimeTest = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = true
				}
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}

		}
		language = StandardLanguage {
			name = "com.yakindu.solidity.Solidity"
			fileExtensions = "sol"
			serializer = {
				generateStub = false
			}
			formatter = {
				generateStub = true
			}
			junitSupport = {
				skipXbaseTestingPackage = true
			}
		}
	}
}
