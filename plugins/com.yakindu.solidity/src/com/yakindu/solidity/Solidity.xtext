/**
 * Copyright (c) 2017 committers of YAKINDU and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 * 
 * Contributors:
 * 	Andreas Muelder - Itemis AG - initial API and implementation
 * 	Karsten Thoms   - Itemis AG - initial API and implementation
 * 	Florian Antony  - Itemis AG - initial API and implementation
 * 	committers of YAKINDU 
 * 
 */

/**
 * 
 * @author andreas muelder
 * @author florian antony
 * 
 */
grammar com.yakindu.solidity.Solidity with org.eclipse.xtext.common.Terminals

generate solidity "http://www.yakindu.com/solidity/Solidity"
import "http://www.yakindu.org/base/expressions/Expressions" as expressions
import "http://www.eclipse.org/emf/2002/Ecore"
import "http://www.yakindu.org/base/base/2.0.0" as base
import "http://www.yakindu.org/base/types/2.0.0" as types

SolidityModel:
	(sourceunit+=SourceUnit)*;

SourceUnit returns types::Package:
	{SourceUnit}
	((pragma+=PragmaDirective)*
	(imports+=ImportDirective)*
	(typeDefinitions+=TypeDefinition)*
	member+=ContractDefinition);

PragmaDirective returns base::DomainElement:
	PragmaSolidityDirective | PragmaExperimentalDirective;

PragmaSolidityDirective returns PragmaDirective:
	{PragmaSolidityDirective} 'pragma' 'solidity' version=PragmaVersion ';';
	
PragmaVersion:
	minorConstraint=PragmaVersionConstraint (majorConstraint=PragmaVersionConstraint)?
;

PragmaVersionConstraint:
	(versionOperator=VersionOperator)? version=VERSION
;

enum VersionOperator :
	ROOF='^' | WAVE='~' | GREATEREQUAL='>=' | SMALLER='<' | GREATER='>' | SMALLEREQUAL='<=' | EQUAL='='
;

PragmaExperimentalDirective returns PragmaDirective:
	{PragmaExperimentalDirective} 'pragma' 'experimental' value=STRING ';';

ImportDirective returns base::DomainElement:
	{ImportDirective} 'import' importedNamespace=STRING ('as' alias=STRING)? ';';

ContractDefinition returns types::ComplexType:
	{ContractDefinition} (abstract?='abstract')? type=ContractType name=ID
	// FIXME: InheritanceSpecifier should be introduced so type arguments can be properly mapped
	('is' superTypes+=TypeSpecifier (argumentList+=SuperTypeArgumentList)? (',' superTypes+=TypeSpecifier (argumentList+=SuperTypeArgumentList)?)*)?
	'{' (features+=ContractPart)* '}';

SuperTypeArgumentList:
	{SuperTypeArgumentList} '(' (arguments+=Expression (',' arguments+=Expression)*)? ')';

enum ContractType:
	CONTRACT='contract' | LIBRARY='library' | INTERFACE='interface';

ContractPart returns types::Declaration:
	StateVariableDefinition | UsingForDeclaration | ModifierDefinition | FunctionDefinition | ConstructorDefinition |
	EventDefinition | FallbackDefinition | ReceiveDefinition |
	TypeDefinition;

UsingForDeclaration returns types::Declaration:
	{UsingForDeclaration} 'using' contract=[ContractDefinition|ID] 'for' ('*' | typeSpecifier=SimpleTypeSpecifier) ';';

StateVariableDefinition returns types::Property:
	{VariableDefinition} typeSpecifier=TypeSpecifier ((visibility=Visibility)? & ('constant')?) name=ID ('='
	initialValue=Expression)? ';';

enum StorageLocation:
	MEMORY='memory' | STORAGE='storage' | CALLDATA='calldata';

VariableDeclaration returns types::Property:
	{VariableDefinition} typeSpecifier=TypeSpecifier (storage=StorageLocation)? name=ID;

enum Visibility:
	PUBLIC='public' | INTERNAL='internal' | PRIVATE='private';

enum Unit:
	SECONDS='seconds' | MINUTES='minutes' | HOURS='hours' | DAYS='days' | WEEKS='weeks' | YEARS='years' | WEI='wei' |
	FINNEY='finney' | SZABO='szabo' | ETHER='ether';

	//TODO This is a hack.
TypeDefinition returns types::Declaration:
	{TypeDefinition} definition=(StructDefinition | EnumDefinition);

StructDefinition returns types::ComplexType:
	{StructDefinition} 'struct' name=ID '{' (features+=VariableDeclaration ';')* '}';

EnumDefinition returns types::EnumerationType:
	'enum' name=ID '{' (enumerator+=Enumerator (',' enumerator+=Enumerator)*)? '}';

Enumerator returns types::Enumerator:
	name=ID;

ModifierDefinition returns types::Operation:
	{ModifierDefinition} 'modifier' name=ID ('(' (parameters+=Parameter (',' parameters+=Parameter)*)? ')')? block=Block;

Block:
	'{' {Block} (statements+=Statement)* '}';

ConstructorDefinition returns types::Operation:
	{ConstructorDefinition} 'constructor' '(' (parameters+=Parameter (',' parameters+=Parameter)*)? ')'
	(modifier+=Modifier)* block=Block;

EventDefinition returns types::Operation:
	{EventDefinition} 'event' name=ID '(' (parameters+=IndexParameter (',' parameters+=IndexParameter)*)? ')' ';';

FunctionDefinition returns types::Operation:
	{FunctionDefinition} 'function' name=ID? '(' (parameters+=Parameter (',' parameters+=Parameter)*)? ')'
	(modifier+=Modifier)* ('returns' '(' (returnParameters+=Parameter (',' returnParameters+=Parameter)*)? ')')? (';' |
	(block=Block));

FallbackDefinition returns types::Operation:
	{FallbackDefinition} 'fallback' '('')'(modifier+=Modifier)*
	block=Block
;

ReceiveDefinition returns types::Operation:
	{ReceiveDefinition} 'receive' '('')'(modifier+=Modifier)*
	block=Block
;

Modifier:
	ModifierInvocation | BuildInModifier;

BuildInModifier:
	type=FunctionModifier;

enum FunctionModifier:
	EXTERNAL='external' | PUBLIC='public' | INTERNAL='internal' | PRIVATE='private' | PURE='pure' | CONSTANT='constant' |
	VIEW='view' | PAYABLE='payable' | OVERRIDE='override' | VIRTUAL='virtual';

ModifierInvocation:
	{ModifierInvocation} reference=[types::Operation|ID] ('(' (arguments+=Argument (',' arguments+=Argument)*)? ')')?;

Parameter returns types::Parameter:
	{Parameter} typeSpecifier=TypeSpecifier (storage=StorageLocation)? (name=ID)?;

IndexParameter returns types::Parameter:
	{IndexParameter} typeSpecifier=TypeSpecifier (indexed?="indexed")? (name=ID)?;

Statement:
	IfStatement | WhileStatement | ForStatement | Block | InlineAssemblyStatement | TryStatement | (ThrowStatement | ReturnStatement |
	DeleteStatement | ContinueStatement | BreakStatement | SimpleStatement | PlaceholderStatement) ';';

ThrowStatement:
	{ThrowStatement} 'throw';

SimpleStatement:
	VariableDefinition | ExpressionStatement;

PlaceholderStatement:
	{PlaceholderStatement} '_';

ReturnStatement:
	{ReturnStatement} 'return' (expression=Expression)?;
	//TODO Why is this an expression in solidity grammar?
DeleteStatement:
	{DeleteStatement} 'delete' expression=Expression;

ContinueStatement:
	{ContinueStatement} 'continue';

BreakStatement:
	{BreakStatement} 'break';

ExpressionStatement:
	{ExpressionStatement} expression=Expression;

InlineAssemblyStatement:
	'assembly' kind=AssemblyKind? block=InlineAssemblyBlock;

enum AssemblyKind:
	EVMASM="evmasm";

VariableDefinitionProperty returns types::Property:
	VariableDefinition;

VariableDefinition:
	('var' identifier=IdentifierList | (typeSpecifier=TypeSpecifier (storage=StorageLocation)? name=ID)) ('='
	initialValue=Expression)?;

IdentifierList:
	{IdentifierList} (identifier+=Identifier | ('(' ((identifier+=Identifier)? (',' (identifier+=Identifier)?)*) ')'));

Identifier returns types::TypedDeclaration:
	{Identifier} name=ID;

IfStatement:
	'if' '(' condition=Expression ')' then=Statement (=> 'else' else=Statement)?;

WhileStatement:
	'while' '(' condition=Expression ')' body=Statement;

ForStatement:
	'for' '(' (initialization=SimpleStatement)? ';' (condition=Expression)? ';' (afterthought=Expression)? ')'
	statement=Statement;

TryStatement:
	'try' expression=Expression ('returns' '('returnParameters+=Parameter (',' returnParameters+=Parameter)* ')')?
	block=Block
	(catchClauses+=CatchClause)+
;
CatchClause:
	'catch' ((identifier=Identifier)? '('parameters+=Parameter (',' parameters+=Parameter)*')')?
	block=Block
;

InlineAssemblyBlock:
	{InlineAssemblyBlock} '{' (items+=AssemblyItem)* '}';

AssemblyItem returns EObject:
	(FunctionalAssemblyExpression | InlineAssemblyBlock | AssemblyLocalBinding | AssemblyAssignment | AssemblyLabel |
	DecimalNumberLiteral | IDLiteral | AddressLiteral);

AssemblyLocalBinding:
	'let' label=AssemblyID ':=' expression=(FunctionalAssemblyExpression | DecimalNumberLiteral);

AssemblyAssignment:
	(leftOperand=AssemblyID ':=' expression=FunctionalAssemblyExpression) | ('=:' rightOpernd=AssemblyID);

AssemblyLabel:
	label=AssemblyID ':';

FunctionalAssemblyExpression:
	=> label=AssemblyID ('(' (parameters+=AssemblyItem (',' parameters+=AssemblyItem)*)? ')')?;

IDLiteral:
	value=AssemblyID;

AssemblyID:
	ID | Keyword;

Keyword:
	'return';

Literal returns expressions::Literal:
	BoolLiteral | DecimalNumberLiteral | AddressLiteral | BinaryLiteral | StringLiteral | NullLiteral;

BoolLiteral returns expressions::BoolLiteral:
	{expressions::BoolLiteral} value=BOOL;

DecimalNumberLiteral:
	value=DECIMAL (unit=Unit)?;

AddressLiteral:
	value=ADDRESS;

BinaryLiteral returns expressions::BinaryLiteral:
	{expressions::BinaryLiteral} value=BINARY;

StringLiteral returns expressions::StringLiteral:
	{expressions::StringLiteral} value=STRING;

NullLiteral returns expressions::NullLiteral:
	{expressions::NullLiteral} 'null';

Expression returns types::Expression:
	AssignmentExpression;

AssignmentExpression returns types::Expression:
	ConditionalExpression ({expressions::AssignmentExpression.varRef=current} operator=AssignmentOperator
	expression=ConditionalExpression)*;

ConditionalExpression returns types::Expression:
	LogicalOrExpression ({expressions::ConditionalExpression.condition=current} '?' trueCase=LogicalOrExpression ':'
	falseCase=LogicalOrExpression)?;

LogicalOrExpression returns types::Expression:
	LogicalAndExpression ({expressions::LogicalOrExpression.leftOperand=current} '||'
	rightOperand=LogicalAndExpression)*;

LogicalAndExpression returns types::Expression:
	LogicalNotExpression ({expressions::LogicalAndExpression.leftOperand=current}
	'&&' rightOperand=LogicalNotExpression)*;

LogicalNotExpression returns types::Expression:
	LogicalRelationExpression | {expressions::LogicalNotExpression} '!'
	operand=LogicalRelationExpression;

LogicalRelationExpression returns types::Expression:
	BitwiseOrExpression
	({expressions::LogicalRelationExpression.leftOperand=current} operator=RelationalOperator
	rightOperand=BitwiseOrExpression)*;

BitwiseOrExpression returns types::Expression:
	BitwiseXorExpression ({expressions::BitwiseOrExpression.leftOperand=current} '|'
	rightOperand=BitwiseXorExpression)*;

BitwiseXorExpression returns types::Expression:
	BitwiseAndExpression ({expressions::BitwiseXorExpression.leftOperand=current} '^'
	rightOperand=BitwiseAndExpression)*;

BitwiseAndExpression returns types::Expression:
	ShiftExpression ({expressions::BitwiseAndExpression.leftOperand=current} '&'
	rightOperand=ShiftExpression)*;

ShiftExpression returns types::Expression:
	NumericalAddSubtractExpression ({expressions::ShiftExpression.leftOperand=current} operator=ShiftOperator
	rightOperand=NumericalAddSubtractExpression)*;

NumericalAddSubtractExpression returns types::Expression:
	NumericalMultiplyDivideExpression
	=> ({expressions::NumericalAddSubtractExpression.leftOperand=current}
	operator=AdditiveOperator
	rightOperand=NumericalMultiplyDivideExpression)*;

NumericalMultiplyDivideExpression returns types::Expression:
	ExponentialExpression
	=> ({expressions::NumericalMultiplyDivideExpression.leftOperand=current}
	operator=MultiplicativeOperator
	rightOperand=ExponentialExpression)*;

ExponentialExpression returns types::Expression:
	NumericalUnaryExpression
	=> ({ExponentialExpression.leftOperand=current}
	'**'
	rightOperand=NumericalUnaryExpression)*;

NumericalUnaryExpression returns types::Expression:
	PostFixUnaryExpression | {NumericalUnaryExpression} operator=UnaryOperator operand=PostFixUnaryExpression;

PostFixUnaryExpression returns types::Expression:
	PrimaryExpression ({expressions::PostFixUnaryExpression.operand=current} operator=PostFixOperator)*;

PrimaryExpression returns types::Expression:
	PrimitiveValueExpression | FeatureCall | NewInstanceExpression | EmitExpression | TupleExpression;

NewInstanceExpression returns expressions::FeatureCall:
	{NewInstanceExpression} 'new' type=TypeSpecifier operationCall?='(' (arguments+=Argument (',' arguments+=Argument)*)?
	')';

EmitExpression returns expressions::FeatureCall:
	{EmitExpression} 'emit' event=[EventDefinition|ID] operationCall?='(' (arguments+=Argument (','
	arguments+=Argument)*)? ')';

TupleExpression returns types::Expression:
	{TupleExpression} 
	(
		('(' (expressions+=(VariableDeclaration | Expression)? (',' (expressions+=(VariableDeclaration | Expression))? )* ) ')') 
		| 
		('[' (expressions+=(VariableDeclaration | Expression) (',' (expressions+=(VariableDeclaration | Expression))? )* ) ']')
	);

PrimitiveValueExpression returns expressions::PrimitiveValueExpression:
	{PrimitiveValueExpression} value=Literal;

FeatureCall returns types::Expression:
	ElementReferenceExpression ({expressions::FeatureCall.owner=current} '.' feature=[types::Declaration]
	(=> operationCall?='(' (arguments+=Argument (',' arguments+=Argument)*)? ')'
	|
	=> (arrayAccess?='[' arraySelector+=Expression ']') => ('[' arraySelector+=Expression ']')*)?)*;

ElementReferenceExpression returns types::Expression:
	{expressions::ElementReferenceExpression} reference=[EObject|ID] (=> operationCall?='(' (((arguments+=SimpleArgument
	(',' arguments+=SimpleArgument)*)?) | ('{' (arguments+=NamedArgument (',' arguments+=NamedArgument)*)? '}')) ')')?
	('[' arraySelector+=Expression ']')*;

Argument returns expressions::Argument:
	SimpleArgument | NamedArgument;

NamedArgument:
	reference=[EObject] ':' value=ConditionalExpression;

SimpleArgument:
	value=ConditionalExpression;

ParenthesizedExpression returns types::Expression:
	{expressions::ParenthesizedExpression} '(' expression=Expression ')';

TypeSpecifier returns types::TypeSpecifier:
	SimpleTypeSpecifier ('payable')?| MappingTypeSpecifier | ArrayTypeSpecifier;

MappingTypeSpecifier:
	{MappingTypeSpecifier} 'mapping' '(' key=TypeSpecifier '=>' value=TypeSpecifier ')';

ArrayTypeSpecifier: //TODO index must be Expression
	{ArrayTypeSpecifier} type=[types::Type|ID] ('[' (index+=PrimitiveValueExpression)? ']')+;

SimpleTypeSpecifier returns TypeSpecifier:
	{SimpleTypeSpecifier} type=[types::Type|ID];

enum AssignmentOperator returns expressions::AssignmentOperator:
	assign='='
	| multAssign='*='
	| divAssign='/='
	| modAssign='%='
	| addAssign='+='
	| subAssign='-='
	| leftShiftAssign='<<='
	| rightShiftAssign='>>='
	| andAssign='&='
	| xorAssign='^='
	| orAssign='|=';

enum UnaryOperator:
	increment="++" | decrement="--" | positive='+' | negative='-' | complement='~';

enum RelationalOperator returns expressions::RelationalOperator:
	smaller='<' | smallerEqual='<=' | greater='>' | greaterEqual='>=' |
	equals='==' | notEquals='!=';

enum MultiplicativeOperator returns expressions::MultiplicativeOperator:
	mul='*' | div='/' | mod='%';

enum ShiftOperator returns expressions::ShiftOperator:
	left='<<'
	| right='>>';

enum AdditiveOperator returns expressions::AdditiveOperator:
	plus='+' | minus='-';

enum PostFixOperator returns expressions::PostFixOperator:
	increment="++" | decrement="--";

terminal ADDRESS returns EBigInteger:
	('0x') ('0'..'9' | 'a'..'f' | 'A'..'F')+;

terminal DECIMAL returns EBigDecimal:
	('0'..'9')+ ('.' ('0'..'9')*)? (('e' | 'E') ('0'..'9')+)?;

terminal VERSION:
	'^' INT . INT (. INT)?;

terminal BOOL returns EBoolean:
	'true' | 'false';

terminal BINARY returns EInt:
	('0') ('b' | 'B') ('0' | '1')+;
