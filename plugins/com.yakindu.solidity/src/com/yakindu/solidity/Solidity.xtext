/**
 * 
 * @author andreas muelder
 * @author florian antony
 * 
 */
grammar com.yakindu.solidity.Solidity with org.yakindu.base.expressions.Expressions

generate solidity "http://www.yakindu.com/solidity/Solidity"
import "http://www.yakindu.org/base/expressions/Expressions" as expressions
import "http://www.eclipse.org/emf/2002/Ecore"
import "http://www.yakindu.org/base/types/2.0.0" as types

SolidityModel:
	(sourceunit+=SourceUnit)*;

SourceUnit returns types::Package:
	{SourceUnit}
	((pragma=PragmaDirective)? &
	(imports+=ImportDirective)* &
	member+=ContractDefinition);

PragmaDirective returns types::PackageMember:
	{PragmaDirective} 'pragma' 'solidity' version=VERSION ';';

ImportDirective returns types::PackageMember:
	{ImportDirective} 'import' importedNamespace=STRING ('as' alias=STRING)? ';';

ContractDefinition returns types::ComplexType:
	{ContractDefinition} type=ContractType name=ID
	('is' superTypes+=[types::ComplexType|QID] (argumentList+=SuperTypeArgumentList)? (','
	superTypes+=[types::ComplexType|QID] ((argumentList+=SuperTypeArgumentList)? ')')?)*)?
	'{' (features+=ContractPart)* '}';

SuperTypeArgumentList:
	{SuperTypeArgumentList} '(' (arguments+=Expression (',' arguments+=Expression)*)? ')';

enum ContractType:
	CONTRACT='contract' | LIBRARY='library' | INTERFACE='interface';

ContractPart returns types::Declaration:
	StateVariableDefinition | UsingForDeclaration | ModifierDefinition | FunctionDefinition | EventDefinition |
	TypeDefinition;

UsingForDeclaration returns types::Declaration:
	{UsingForDeclaration} 'using' contract=[ContractDefinition|ID] 'for' ('*' | typeSpecifier=SimpleTypeSpecifier) ';';

StateVariableDefinition returns types::Property:
	{VariableDefinition} typeSpecifier=TypeSpecifier ((visibility=Visibility)? & ('constant')?) name=ID ('='
	initialValue=Expression)? ';';

@ Override TypeSpecifier:
	SimpleTypeSpecifier | MappingTypeSpecifier | ArrayTypeSpecifier;

MappingTypeSpecifier:
	{MappingTypeSpecifier} 'mapping' '(' key=TypeSpecifier '=>' value=TypeSpecifier ')';

ArrayTypeSpecifier: //TODO index must be Expression
	{ArrayTypeSpecifier} type=[types::Type|QID] ('[' (index+=PrimitiveValueExpression)? ']')+;

SimpleTypeSpecifier returns TypeSpecifier:
	type=[types::Type|QID];

enum StorageLocation:
	MEMORY='memory' | STORAGE='storage';

VariableDeclaration returns types::Property:
	{VariableDefinition} typeSpecifier=TypeSpecifier (storage=StorageLocation)? name=ID;

enum Visibility:
	PUBLIC='public' | INTERNAL='internal' | PRIVATE='private';

enum Unit:
	SECONDS='seconds' | MINUTES='minutes' | HOURS='hours' | DAYS='days' | WEEKS='weeks' | YEARS='years' | WEI='wei' |
	FINNEY='finney' | SZABO='szabo' | ETHER='ether';

	//TODO This is a hack.
TypeDefinition returns types::Declaration:
	{TypeDefinition} definition=(StructDefinition | EnumDefinition);

StructDefinition returns types::ComplexType:
	{StructDefinition} 'struct' name=ID '{' (features+=VariableDeclaration ';')* '}';

EnumDefinition returns types::EnumerationType:
	'enum' name=ID '{' (enumerator+=Enumerator (',' enumerator+=Enumerator)*)? '}';

Enumerator returns types::Enumerator:
	name=ID;

ModifierDefinition returns types::Operation:
	{ModifierDefinition} 'modifier' name=ID ('(' (parameters+=Parameter (',' parameters+=Parameter)*)? ')')? block+=Block;

Block:
	'{' {Block} (statements+=Statement)* '}';

@ Override Literal returns expressions::Literal:
	BoolLiteral | DecimalNumberLiteral | AddressLiteral | BinaryLiteral | StringLiteral | NullLiteral;

DecimalNumberLiteral:
	value=DECIMAL (unit=Unit)?;

@ Override
DoubleLiteral returns expressions::DoubleLiteral:
	{DoubleLiteral} value=DOUBLE (unit=Unit)?;

AddressLiteral:
	value=ADDRESS;

FunctionDefinition returns types::Operation:
	{FunctionDefinition} 'function' name=ID? '(' (parameters+=Parameter (',' parameters+=Parameter)*)? ')'
	(modifier+=Modifier)* ('returns' '(' (returnParameters+=Parameter (',' returnParameters+=Parameter)*)? ')')? (';' |
	(block=Block));

Modifier:
	ModifierInvocation | BuildInModifier;

BuildInModifier:
	type=FunctionModifier;

enum FunctionModifier:
	EXTERNAL='external' | PUBLIC='public' | INTERNAL='internal' | PRIVATE='private' | PURE='pure' | CONSTANT='constant' |
	VIEW='view' | PAYABLE='payable';

ModifierInvocation:
	{ModifierInvocation} reference=[types::Operation|ID] ('(' (arguments+=Argument (',' arguments+=Argument)*)? ')')?;

Parameter returns types::Parameter:
	{Parameter} typeSpecifier=TypeSpecifier (storage=StorageLocation)? (name=ID)?;

IndexParameter returns types::Parameter:
	{IndexParameter} typeSpecifier=TypeSpecifier (indexed?="indexed")? (name=ID)?;

EventDefinition returns types::Operation:
	{EventDefinition} 'event' name=ID '(' (parameters+=IndexParameter (',' parameters+=IndexParameter)*)? ')' ';';

Statement:
	IfStatement | WhileStatement | ForStatement | Block | InlineAssemblyStatement | (ThrowStatement | ReturnStatement |
	DeleteStatement | ContinueStatement | BreakStatement | SimpleStatement | PlaceholderStatement) ';';

ThrowStatement:
	{ThrowStatement} 'throw';

SimpleStatement:
	VariableDefinition | ExpressionStatement;

PlaceholderStatement:
	{PlaceholderStatement} '_';

ReturnStatement:
	{ReturnStatement} 'return' (expression=Expression)?;
	//TODO Why is this an expression in solidity grammar?
DeleteStatement:
	{DeleteStatement} 'delete' expression=Expression;

ContinueStatement:
	{ContinueStatement} 'continue';

BreakStatement:
	{BreakStatement} 'break';

ExpressionStatement:
	expression=Expression;

InlineAssemblyStatement:
	'assembly' kind=AssemblyKind? block=InlineAssemblyBlock;

enum AssemblyKind:
	EVMASM="evmasm";

@ Override NumericalMultiplyDivideExpression returns expressions::Expression:
	NumericalUnaryExpression => ({NumericalMultiplyDivideExpression.leftOperand=current} operator=MultiOperator
	rightOperand=NumericalUnaryExpression)*;

enum MultiOperator:
	mul='*' | div='/' | mod='%' | exp="**";

	// Remove TypeCast Expression
@ Override NumericalUnaryExpression returns expressions::Expression:
	PostFixUnaryExpression | {NumericalUnaryExpression} operator=UnaryOperatorEx operand=PostFixUnaryExpression;

PostFixUnaryExpression returns expressions::Expression:
	PrimaryExpression ({PostFixUnaryExpression.operand=current} operator=PostFixOperator)*;

enum PostFixOperator:
	increment="++" | decrement="--";

enum UnaryOperatorEx:
	increment="++" | decrement="--" | positive='+' | negative='-' | complement='~';

@ Override
ElementReferenceExpression returns expressions::ElementReferenceExpression:
	{expressions::ElementReferenceExpression} reference=[EObject|ID] (=> operationCall?='(' (arguments+=Argument (','
	arguments+=Argument)*)? ')')? ('[' arraySelector+=Expression ']')*;

	//Disables named parameter calls
@ Override
Argument:
	value=ConditionalExpression;

@ Override PrimaryExpression returns expressions::Expression:
	PrimitiveValueExpression | FeatureCall | NewInstanceExpression | TupleExpression;

NewInstanceExpression returns expressions::FeatureCall:
	{NewInstanceExpression} 'new' type=TypeSpecifier operationCall?='(' (arguments+=Argument (',' arguments+=Argument)*)?
	')';

TupleExpression returns expressions::Expression:
	{TupleExpression} (('(' (expressions+=Expression (',' expressions+=Expression)*)? ')') | ('[' (expressions+=Expression
	(',' expressions+=Expression)*)? ']'));

VariableDefinitionProperty returns types::Property:
	VariableDefinition;

VariableDefinition:
	('var' identifier=IdentifierList | (typeSpecifier=TypeSpecifier (storage=StorageLocation)? name=ID)) ('='
	initialValue=Expression)?;

IdentifierList:
	{IdentifierList} (identifier+=Identifier | ('(' ((identifier+=Identifier)? (',' (identifier+=Identifier)?)*) ')'));

Identifier returns types::Declaration:
	{Identifier} name=ID;

IfStatement:
	'if' '(' condition=Expression ')' then=Statement (=> 'else' else=Statement)?;

WhileStatement:
	'while' '(' condition=Expression ')' body=Statement;

ForStatement:
	'for' '(' (initialization=SimpleStatement)? ';' (condition=Expression)? ';' (afterthought=Expression)? ')'
	statement=Statement;

InlineAssemblyBlock:
	{InlineAssemblyBlock} '{' (items+=AssemblyItem)* '}';

AssemblyItem returns EObject:
	(FunctionalAssemblyExpression | InlineAssemblyBlock | AssemblyLocalBinding | AssemblyAssignment | AssemblyLabel |
	DecimalNumberLiteral | IDLiteral | AddressLiteral);

AssemblyLocalBinding:
	'let' label=ID ':=' expression=(FunctionalAssemblyExpression | DecimalNumberLiteral);

AssemblyAssignment:
	(leftOperand=ID ':=' expression=FunctionalAssemblyExpression) | ('=:' rightOpernd=ID);

AssemblyLabel:
	label=ID ':';

FunctionalAssemblyExpression:
	=> label=ID ('(' (parameters+=AssemblyItem (',' parameters+=AssemblyItem)*)? ')')?;

IDLiteral:
	value=ID;

terminal ADDRESS returns EBigInteger:
	('0x') ('0'..'9' | 'a'..'f' | 'A'..'F')+;

terminal DECIMAL returns EBigDecimal:
	('0'..'9')+ ('.' ('0'..'9')*)? (('e' | 'E') ('0'..'9')+)?;

terminal VERSION:
	'^' INT . INT . INT;

